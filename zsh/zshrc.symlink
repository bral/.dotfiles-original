# -*- mode: shell-script; -*-

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# all of our zsh files
typeset -U config_files
config_files=($ZSH/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

autoload -U zmv

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

pathdirs=(
  $HOME/bin
  $HOME/bin/*
  $HOME/.rvm/bin
  /bin
  /sbin
  /opt/bin
  /usr/local/bin
  /usr/local/heroku/bin
  /usr/local/opt/autoconf/bin
  /usr/local/lib/python2.7/site-packages
  /usr/bin
  /usr/sbin
)

for dir in $pathdirs; do
  test -d $dir && path+=$dir
done

# source optional profile
test -f ~/.zshprofile && source ~/.zshprofile

# use gnu manpages
export MANPATH="/usr/local/opt/coreutils/libexec/gnuman:$MANPATH"
export HELPDIR=/usr/local/share/zsh/help

# this lets `read` and `vared` backspace properly
if [[ $(ps -o comm= $PPID) = iterm ]]; then
  stty erase '^?'
fi
